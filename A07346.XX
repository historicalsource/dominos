	.TITLE	DOMINO
TEMP3	=16
FTIM1	=20
FTIM2	=21
MASK	=22
SNUM	=23
SOUND	=24
CRASH	=26
FRAME	=40
TIMER	=41
FLAG1	=42
POS1	=44
POS2	=46
PPOS1	=50
PPOS2	=52
SBI1	=4054
SBI2	=4055
ST	=4056
CLICK	=6020
TEST	=14000
TESTV	=252
AMODE	=6000
TREST	=6200
NREST	=7400
SB1	=6060
SB2	=6100
GSW	=4060
CSW	=4061
COINAD	=4100
LFREQ	=4062
PWALL1	=54
PWALL2	=55
ACOD1	=56
ACOD2	=57
ADIR1	=60
ADIR2	=61
HEATS	=62
FLAG2	=63
HSCR	=64
TPOS1	=2045
TPOS2	=2065
SPOS1	=2747
SPOS2	=2730
STIM1	=100
TAMT	=101
STIM2	=102
AFLAG	=103
COINS	=104
TEMP2	=106
T1	=110
T2	=111
T3	=112
T4	=113
UP1	=114
UP2	=115
UP3	=116
UP4	=117
SCR1	=120
TONE	=121
SCL1	=122
ONCE	=123
RTEST	=124
CREDIT	=66
PTR	=70
TEMP1	=72
TOG	=74
WALL1	=75
WALL2	=76
PLAYFIELD=2000
SW1	=4030
SW2	=4050
	.ASECT
	.=30000
	.REPT	32000-.
	.BYTE	0
	.ENDM
PWRON:	SEI			;POWER ON RESET
	CLD
	LDX	I,177		;SET UP STACK
	TXS
	LDA	A,TEST
	CMP	I,TESTV		;DIAGNOSTIC PRESENT?
	BEQ	TESTVJ		;YES, GO TO TEST
	BNE	MAINT		;NO, GO TO MAIN LOOP
TESTVJ:	JSR	A,TEST
MAINT:	JSR	A,SETUP		;CLEAR CREDIT
MAIN:	JSR	A,ATRACT
	JSR	A,START
	JMP	A,MAIN
;ATTRACT SUBROUTINE
ATRACT:	LDA	I,0
	STA	Z,CRASH
	STA	A,AMODE+1	;TURN OFF SOUND
	STA	A,SB1
	STA	A,SB2
	STA	Z,AFLAG		;SET ATTRACT FLAG
	JSR	A,BLANK		;BLANK SCREEN
	JSR	A,INITL		;INITIALIZE
	STA	A,TREST		;PULSE WATCHDOG
	JSR	A,SCORE		;PUT UP SCORE
	LDA	I,104		;OUTPUT COIN MESSAGE
	BIT	A,CSW
	BPL	A70
	LDA	I,102
	BVC	A70
	LDA	I,103
A70:	JSR	A,MESS
	LDA	I,107		;OUTPUT GAME PTS. TO WIN
	JSR	A,MESS
	LDA	Z,HSCR
	ORA	I,260
	STA	A,PLAYFIELD+1447
	LDA	I,101		;OUTPUT GAME OVER
	JSR	A,MESS
A1:	JSR	A,PLAY
	LDA	A,ST		;SELF-TEST?
	BMI	S,+5		;NO
	JSR	A,TRAM		;YES, GO TO SELF-TEST
	LDA	Z,CREDIT
	BNE	A2
	LDA	Z,TEMP2
	BMI	ATRACT
	JSR	A,WAIT
	JMP	A,A1
A2:	LDX	I,217
A15:	LDA	A,SBI1		;START #1?
	BMI	A55		;NO
	LDA	Z,CREDIT	;YES, ANY CREDIT?
	BNE	S,+5		;NO, LEAVE LOOP
A200:	JMP	A,A20
	STA	A,CLICK+1
	LDA	Z,AFLAG		;WHAT MODE?
	BNE	A6
	LDA	I,1		;MAKE IT 1-PLAYER
	STA	Z,AFLAG
A4:	DEC	Z,CREDIT
	JMP	A,A2		;RESET TIMER
A6:	CMP	I,1
	BEQ	A5
	CMP	I,2
	BNE	A200
A8:	LDA	I,3
	STA	Z,AFLAG
	BPL	A4
A55:	STA	A,CLICK
A5:	LDA	A,SBI2		;START #2?
	BMI	A3		;NO
	LDA	Z,CREDIT	;YES, ANY CREDIT
	BEQ	A200		;NO
	STA	A,CLICK+1
	LDA	Z,AFLAG		;WHAT MODE?
	BNE	A7
	LDA	I,2
	STA	Z,AFLAG
	BPL	A4
A7:	CMP	I,1
	BEQ	A8
	CMP	I,2
	BEQ	A3
	BNE	A200
A3:	LDA	Z,ONCE
	BNE	A73
	JSR	A,BLANK		;CLEAR SCREEN
	JSR	A,INITL
	JSR	A,SCORE
	INC	Z,ONCE
	LDA	I,104		;OUTPUT COIN MESSAGE
	BIT	A,CSW
	BPL	A72
	LDA	I,102
	BVC	A72
	LDA	I,103
A72:	JSR	A,MESS
	LDA	I,107		;OUTPUT GAME PTS. TO WIN
	JSR	A,MESS
	LDA	Z,HSCR
	ORA	I,260
	STA	A,PLAYFIELD+1447
A73:	LDA	Z,CREDIT	;OUTPUT CREDIT MESSAGES
	BEQ	A71
	LDA	I,105
	JSR	A,MESS
	LDA	Z,CREDIT
	CMP	I,2
	BCC	S,+4
	LDA	I,2
	ORA	I,260
	STA	A,PLAYFIELD+526
	CMP	I,261
	BNE	A71
	LDA	I,0
	STA	A,PLAYFIELD+536
A71:	TXA
	AND	I,10
	BNE	A10
	LDA	Z,AFLAG
	BEQ	S,+5
	JSR	A,TICK
	LDA	I,206
	JSR	A,MESS
	LDA	I,210
	JSR	A,MESS
	LDA	Z,AFLAG
	BNE	A11
	STA	A,SB1
	BEQ	A12
A10:	STA	A,SB1+1
	STA	A,SB2+1
	LDA	I,106
	JSR	A,MESS
	LDA	I,110
	JSR	A,MESS
	BEQ	A14
A11:	CMP	I,1
	BNE	A13
A12:	STA	A,SB2
	BEQ	A14
A13:	CMP	I,2
	BNE	A14
	STA	A,SB1
A14:	JSR	A,WAIT
	INX
	BEQ	A20
	JMP	A,A15
A20:	LDA	Z,AFLAG
	BNE	S,+5
	JMP	A,A2
	STA	A,CLICK
	STA	A,SB1+1		;MAKE SURE PROPER LITES
	STA	A,SB2+1		;ARE ON DURING GAME
	CMP	I,1
	BNE	A40
	STA	A,SB2
A40:	CMP	I,2
	BNE	A41
	STA	A,SB1
A41:	RTS

;START SUBROUTINE
START:	JSR	A,BLANK		;BLANK SCREEN
	JSR	A,INITL		;INITIALIZE
	LDA	I,0
	STA	Z,HEATS
	STA	Z,SCR1
	STA	Z,SCL1
	STA	Z,CRASH
	STA	Z,TONE
	LDA	I,4
	STA	Z,SOUND
	JSR	A,SCORE
	STA	A,AMODE		;TURN ON SOUND
S2:	JSR	A,PLAY
	LDA	Z,TEMP2
	BMI	S750
S751:	JSR	A,WAIT
	JMP	A,S2
S750:	STA	A,CLICK
	LDA	I,0
	STA	Z,CRASH
	LDX	I,217		;FREEZE SCREEN FOR AWHILE
S79:	JSR	A,WAIT
	TXA
	LDY	Z,HSCR		;FLASH GAME OVER
	CPY	Z,SCR1
	BEQ	S,+6		;YES
	CPY	Z,SCL1
	BNE	S752		;NO
	LDY	I,101
	AND	I,30
	BNE	S,+4
	LDY	I,201
	TYA
	JSR	A,MESS
S752:	INX
	BNE	S79
	LDA	Z,SCR1		;GAME FINISHED?
	CMP	Z,HSCR
	BEQ	S78		;YES
	LDA	Z,SCL1
	CMP	Z,HSCR
	BEQ	S78
	JSR	A,BLANK
	JSR	A,INITL		;NO, RESTART GAME
	JSR	A,SCORE
	LDA	I,4
	STA	Z,SOUND
	JMP	A,S2
S78:	RTS			;ALL DONE, RETURN TO MAINLINE
;PLAY SUBROUTINE
PLAY:	LDA	Z,FLAG1
	BNE	P41
	LDA	Z,FLAG2
	BNE	P41
	LDA	Z,TONE		;IN NORMAL PLAY
	BEQ	P67		;MAKE SOUND WHOOSH
	LDA	I,4
	SEC
	SBC	Z,TONE
	DEC	Z,TONE
P67:	STA	Z,CRASH
	BPL	P40
P41:	LDA	Z,CRASH
	BEQ	P40
	DEC	Z,CRASH
P40:	JSR	A,SCAN		;LOOK AT SWITCHES
	LDA	Z,FLAG1		;EITHER PLAYER COLLIDED?
	BNE	P10		;YES
	LDA	Z,FLAG2
	BNE	P10
	LDA	Z,TIMER		;TIME TO UPDATE?
	BEQ	P1		;YES
	DEC	Z,TIMER		;NO, DECREMENT TIMER
	JMP	A,P10
P1:	LDA	Z,TAMT		;REST TIMER
	STA	Z,TIMER
	LDA	Z,TOG		;WHICH PLAYER?
	BNE	P2		;PLAYER 2
;PLAYER 1 (BLACK)
	INC	Z,TOG		;BUMP TOGGLE
	LDA	Z,FLAG1		;GOING FORWARD
	BNE	P10		;NO
	JSR	A,FOR1		;YES, DO SO
	JMP	A,P10
;PLAYER 2 (WHITE)
P2:	LDA	I,0		;RESET TOGGLE
	STA	Z,TOG
	LDA	Z,FLAG2		;GOING FORWARD
	BNE	P10		;NO
	JSR	A,FOR2		;YES, DO SO
P10:	LDA	Z,FLAG1
	BEQ	P20
	CMP	I,2		;ALL DONE?
	BNE	P11		;NO
	JMP	A,S75		;ALL DONE
P11:	CMP	I,1		;TOPPLING?
	BNE	P12		;NO
	JSR	A,TOP1		;YES
	JMP	A,P20
P12:	JSR	A,COL1		;FLASHING COLLISION
P20:	LDA	Z,FLAG2
	BEQ	S80
	CMP	I,2		;ALL DONE?
	BNE	P13		;NO
	JMP	A,S75		;ALL DONE
P13:	CMP	I,1		;TOPPLING?
	BNE	P14		;NO
	JSR	A,TOP2		;YES
	JMP	A,S80
P14:	JSR	A,COL2		;FLASHING COLLISION
S80:	INC	Z,FRAME		;BUMP FRAME COUNTER
	LDA	Z,FRAME		;TIME TO SPEEDUP?
	AND	Z,MASK
	BNE	S81		;NO
	LDA	Z,TAMT		;YES
	CMP	I,5		;MAX SPEEDUP
	BEQ	S81		;YES
	DEC	Z,TAMT		;NO, SPEED IT UP
S81:	LDA	I,0
	BEQ	S,+4
S75:	LDA	I,377
	STA	Z,TEMP2
	RTS
FOR1:	LDA	Z,AFLAG		;SUPPRESS SOUND IN
	BEQ	F2		;ATTRACT MODE
	LDA	I,0
	STA	Z,SOUND
	LDA	I,3
	STA	Z,TONE
F2:	LDA	Z,PWALL1	;FIRST TIME?
	BNE	F3		;NO
	LDY	Z,ADIR1		;YES
	LDX	AY,BTAB
	BPL	F4
F3:	LDX	I,0
F5:	CMP	AX,CTAB		;GET CORRECT DOMINO INDEX
	BEQ	F4
	INX
	BPL	F5
F4:	LDY	Z,ADIR1		;ACCESS CORRECT
	BNE	F6		;DIRECTION TABLE
	LDA	AX,TAB1
	BNE	F10
F6:	CPY 	I,1
	BNE	F7
	LDA	AX,TAB2
	BNE	F10
F7:	CPY	I,2
	BNE	F8
	LDA	AX,TAB3
	BNE	F10
F8:	LDA	AX,TAB4
F10:	LDY	I,0
	STA	NY,POS1		;OUTPUT WALL SECTION
	STA	Z,PWALL1
	LDY	Z,POS1		;SAVE POSITION
	STY	Z,PPOS1
	LDY	Z,POS1+1
	STY	Z,PPOS1+1
	LDA	Z,ACOD1
	JSR	A,NPOS1		;UPDATE POSITION
	LDY	I,0		;CHECK FOR EMINENT COLLISIONS
	LDA	NY,POS1
	BEQ	F11
F44:	STA	Z,WALL1
	LDA	Z,POS1
	CMP	Z,POS2
	BNE	F43
	LDA	Z,POS1+1
	CMP	Z,POS2+1
	BNE	F43
	LDA	I,377
	STA	Z,FLAG2
F43:	LDA	I,377		;SET FLAG FOR FLASHING
	STA	Z,FLAG1
	LDA	I,77
	STA	Z,MASK		;SET MASK FOR SPEED-UP
	LDA	Z,AFLAG		;SUPPRESS CRASH IN
	BEQ	F11		;ATTRACT MODE
	LDA	I,8.
	STA	Z,SOUND
	LDA	I,17
	STA	Z,CRASH
F11:	LDA	Z,ACOD1		;OUTPUT NEW ARROW
	STA	NY,POS1
F1:	RTS
FOR2:	LDA	Z,AFLAG		;SUPPRESS SOUND IN
	BEQ	F21		;ATTRACT MODE
	LDA	I,4
	STA	Z,SOUND
	LDA	I,3
	STA	Z,TONE
F21:	LDA	Z,PWALL2	;FIRST TIME?
	BNE	F22		;NO
	LDY	Z,ADIR2		;YES
	LDX	AY,BTAB
	BPL	F23
F22:	AND	I,177
	LDX	I,0
F24:	CMP	AX,CTAB		;GET CORRECT DOMINO INDEX
	BEQ	F23
	INX
	BPL	F24
F23:	LDY	Z,ADIR2		;ACCESS CORRECT
	BNE	F25		;DIRECTION TABLE
	LDA	AX,TAB1
	BNE	F30
F25:	CPY	I,1
	BNE	F26
	LDA	AX,TAB2
	BNE	F30
F26:	CPY	I,2
	BNE	F27
	LDA	AX,TAB3
	BNE	F30
F27:	LDA	AX,TAB4
F30:	LDY	I,0
	ORA	I,200		;OR IN COLOR CODE
	STA	NY,POS2		;OUTPUT WALL SECTION
	STA	Z,PWALL2
	LDY	Z,POS2		;SAVE POSITION
	STY	Z,PPOS2
	LDY	Z,POS2+1
	STY	Z,PPOS2+1
	LDA	Z,ACOD2
	JSR	A,NPOS2		;UPDATE POSITION
	LDY	I,0
	LDA	NY,POS2
	BEQ	F31
F41:	STA	Z,WALL2
	LDA	Z,POS1
	CMP	Z,POS2
	BNE	F40
	LDA	Z,POS1+1
	CMP	Z,POS2+1
	BNE	F40
	LDA	I,377
	STA	Z,FLAG1
F40:	LDA	I,377		;SET FLAG FOR FLASHING
	STA	Z,FLAG2
	LDA	I,77	
	STA	Z,MASK		;SET MASK FOR SPEED-UP
	LDA	Z,AFLAG		;SUPPRESS CRASH
	BEQ	F31		;IN ATTRACT MODE
	LDA	I,8.
	STA	Z,SOUND
	LDA	I,17
	STA	Z,CRASH
F31:	LDA	Z,ACOD2		;OUTPUT NEW ARROW
	STA	NY,POS2
F20:	RTS
COL1:	LDY	I,0
	LDA	Z,FTIM1		;FLASH TIMER STILL ACTIVE?
	BEQ	CL1		;NO
	DEC	Z,FTIM1		;YES, DECREMENT IT
	LDA	Z,FTIM1		;TIME TO FLASH?
	AND	I,2
	BNE	CL2
	LDA	Z,ACOD1		;FLASH ARROW
	STA	NY,POS1
	RTS
CL2:	LDA	Z,WALL1		;FLASH WALL
	STA	NY,POS1
	RTS
CL1:	LDA	I,1		;FLASHING ALL DONE
	STA	Z,FLAG1		;SET FLAG TO TOPPLE
	LDA	Z,WALL1		;RESTORE WALL
	LDX	Z,FLAG2		;IF BOTH COLLIDE
	BPL	S,+4		;RESET TO BLANK WALL
	LDA	I,0
	STA	NY,POS1
	LDA	Z,PPOS1
	STA	Z,POS1
	LDA	Z,PPOS1+1
	STA	Z,POS1+1
	RTS
COL2:	LDY	I,0
	LDA	Z,FTIM2		;FLASH TIMER STILL ACTIVE?
	BEQ	CL4		;NO
	DEC	Z,FTIM2		;YES, DECREMENT IT
	LDA	Z,FTIM2		;TIME TO FLASH?
	AND	I,2
	BNE	CL3
	LDA	Z,ACOD2		;FLASH ARROW
	STA	NY,POS2
	RTS
CL3:	LDA	Z,WALL2		;FLASH WALL
	STA	NY,POS2
	RTS
CL4:	LDA	I,1		;FLASHING ALL DONE
	STA	Z,FLAG2		;SET FLAG TO TOPPLE
	LDA	Z,WALL2		;RESTORE WALL
	LDX	Z,FLAG1
	BPL	S,+4
	LDA	I,0
	STA	NY,POS2
	LDA	Z,PPOS2
	STA	Z,POS2
	LDA	Z,PPOS2+1
	STA	Z,POS2+1
	RTS
TOP1:	LDA	A,STIM1		;TIME TO TOPPLE?
	BEQ	TO1		;YES
	DEC	Z,STIM1		;NO, DECREMENT TIMER
	RTS
TO1:	LDY	I,0
	LDA	NY,POS1		;READ CURRENT POSITION
	STA	Z,TEMP1
	ORA	I,1
	STA	NY,POS1		;TOPPLE DOMINO
	LDA	Z,AFLAG
	BEQ	TO3
	JSR	A,TICK
TO3:	LDA	Z,POS1		;CURRENT POSITION =
	CMP	I,SPOS1&377	;INITIAL POSITION?
	BNE	TO2		;NO
	LDA	Z,POS1+1
	CMP	I,SPOS1&177400/256.
	BNE	TO2		;NO
	LDA	I,2		;YES, ALL DONE
	STA	Z,FLAG1		;SET FLAG TO DONE
	JSR	A,ADDL2		;GIVE PLAYER 2 A POINT
	JSR	A,SCORE
	RTS			;RETURN
TO2:	LDA	I,2		;RESET TOPPLE TIMER
	STA	Z,STIM1
	LDA	Z,TEMP1
	JSR	A,UPOS1
	RTS
TOP2:	LDA	Z,STIM2		;TIME TO TOPPLE?
	BEQ	TO10		;YES
	DEC	Z,STIM2		;NO, DECREMENT TIMER
	RTS
TO10:	LDY	I,0
	LDA	NY,POS2		;READ CURRENT POSITION
	STA	Z,TEMP1
	ORA	I,1
	STA	NY,POS2		;TOPPLE DOMINO
	LDA	Z,AFLAG		;SUPPRESS CLICK
	BEQ	TO11		;IN ATTRACT MODE
	JSR	A,TICK
TO11:	LDA	Z,POS2		;CURRENT POSITION =
	CMP	I,SPOS2&377	;INITIAL POSITION?
	BNE	TO12		;NO
	LDA	Z,POS2+1
	CMP	I,SPOS2&177400/256.
	BNE	TO12		;NO
	LDA	I,2		;YES, ALL DONE
	STA	Z,FLAG2		;SET FLAG TO DONE
	JSR	A,ADDL1		;GIVE PLAYER 1 A POINT
	JSR	A,SCORE
	RTS
TO12:	LDA	I,2		;RESET TOPPLE TIMER
	STA	Z,STIM2
	LDA	Z,TEMP1
	JSR	A,UPOS2		;UPDATE POSITION
	RTS
;
;MESSAGE ROUTINE
;	ON ENTRY A-REG = BITS 0-5 = # OF MESSAGE
;		       = BIT 6 = 1 WHITE
;				 0 BLACK
;		       = BIT 7 = 1 BLANK MESSAGE
;			         0 WRITE MESSAGE
;
MESS:	STX	Z,TEMP3
	STY	Z,TEMP3+1
	LDX	I,0
	STX	Z,PTR
	STX	Z,PTR+1
	CLC
	ROL
	ROL	Z,PTR		;SAVE R/W BIT
	ROL
	ROL	Z,PTR+1		;SAVE COLOR BIT
	LSR
	TAX
	LDA	AX,MESG
	STA	Z,TEMP1
	LDA	AX,MESG+1
	STA	Z,TEMP1+1
	LDA	AX,POSM
	STA	Z,TEMP2
	LDA	AX,POSM+1
	STA	Z,TEMP2+1
	LDY	I,0
M1:	LDA	NY,TEMP1
	BEQ	M5
	LDX	Z,PTR
	BNE	M2
	CMP	I,40
	BNE	M3
M2:	LDA	I,0
	BEQ	M4
M3:	LDX	Z,PTR+1
	BEQ	M4
	ORA	I,200
M4:	STA	NY,TEMP2
	INY
	BPL	M1
M5:	LDX	Z,TEMP3
	LDY	Z,TEMP3+1
	RTS
TICK:	STA	A,CLICK+1	;GENERATES A CLICK
	STY	Z,TEMP3
	STX	Z,TEMP3+1
	LDY	I,0
	LDX	I,1
TK1:	DEY			;WAIT 5 MSEC
	BNE	S,-1
	DEX
	BNE	TK1
	STA	A,CLICK
	LDY	Z,TEMP3
	LDX	Z,TEMP3+1
	RTS
SETUP:	LDA	I,0
	STA	Z,COINS
	STA	Z,CREDIT
	STA	Z,SCR1
	STA	Z,SCL1
	STA	A,CLICK
	RTS
WAIT:	BIT	A,COINAD	;CONTINUALLY LOOK FOR COINS
	BPL	WA2
	BVS	WA1
WA2:	JSR	A,COIN
	JSR	A,LINE
WA1:	LDA	A,AMODE
	ROL
	ROL
	BPL	WAIT
	STA	A,TREST		;PULSE WATCHDOG
	RTS
INITL:	LDA	I,77		;SETUP ARROWS
	STA	A,SPOS1
	LDA	I,275
	STA	A,SPOS2
	LDA	I,SPOS1&377	;SETUP INITIAL POSITIONS
	STA	Z,POS1
	LDA	I,SPOS1&177400/256.
	STA	Z,POS1+1
	LDA	I,SPOS2&377
	STA	Z,POS2
	LDA	I,SPOS2&177400/256.
	STA	Z,POS2+1
	LDA	I,1
	STA	Z,ADIR1
	LDA	I,3
	STA	Z,ADIR2
	LDA	I,77
	STA	Z,ACOD1
	LDA	I,275
	STA	Z,ACOD2
	LDA	I,14.		;SET UP TIMER
	STA	Z,TIMER
	STA	Z,TAMT
	LDA	I,8.
	STA	Z,FTIM1
	STA	Z,FTIM2
	LDA	I,2
	STA	Z,STIM1
	STA	Z,STIM2
	LDA	I,177
	STA	Z,MASK
	LDA	A,GSW		;SET GAME LENGTH
	AND	I,300
	CLC
	ROL
	ROL
	ROL
	ADC	I,3
	STA	Z,HSCR
	LDA	I,0
	STA	Z,FLAG1
	STA	Z,FLAG2
	STA	Z,TOG
	STA	Z,ONCE
	STA	Z,SNUM
	STA	Z,PWALL1
	STA	Z,PWALL2
	LDA	I,PLAYFIELD+137&377
	STA	Z,PTR
	LDA	I,PLAYFIELD+137&177400/256.
	STA	Z,PTR+1
	LDX	I,0
I3:	LDY	I,0
	LDA	I,272
	STA	NY,PTR
	INY
	AND	I,177
	STA	NY,PTR
	LDA	Z,PTR
	CLC
	ADC	I,40
	STA	Z,PTR
	LDA	Z,PTR+1
	ADC	I,0
	STA	Z,PTR+1
	INX
	CPX	I,25.
	BNE	I3
	LDX	I,0
	LDA	I,72
I1:	STA	AX,PLAYFIELD+100
	STA	AX,PLAYFIELD+1540
	CPX	I,16.
	BNE	I2
	ORA	I,200
I2:	INX
	CPX	I,32.
	BNE	I1
	LDA	I,0
	JSR	A,MESS
	RTS
BLANK:	LDA	I,4		;BLANKS SCREEN
	STA	Z,TEMP2
	LDA	I,PLAYFIELD/256.
	STA	Z,PTR+1
	LDA	I,0
	STA	Z,PTR
	TAY
B1:	STY	Z,TEMP2+1	;CHECK FOR COINS
	JSR	A,COIN		;WHILE BLANKING SCREEN
	LDY	Z,TEMP2+1
	LDA	I,0
	STA	NY,PTR
	LDX	Z,PTR+1
	CPX	I,7
	BCC	S,+7
	CPY	I,200
	BCC	S,+3
	RTS
	INY
	BNE	B1
	INC	Z,PTR+1
	DEC	Z,TEMP2
	BNE	B1
	RTS
SCORE:	LDA	Z,SCR1		;OUTPUT SCORE TO SCREEN
	ORA	I,60
	STA	A,TPOS1+3
	LDA	Z,SCL1
	ORA	I,260
	STA	A,TPOS2+3
	RTS
ADDL1:	LDA	Z,AFLAG		;ADDS 1 TO PLAYER 1'S SCORE
	BEQ	AL1
	LDA	Z,SCR1
	CLC
	ADC	I,1
	STA	Z,SCR1
AL1:	RTS
ADDL2:	LDA	Z,AFLAG		;ADDS 1 TO PLAYER 2'S SCORE
	BEQ	AL2
	LDA	Z,SCL1
	CLC
	ADC	I,1
	STA	Z,SCL1
AL2:	RTS
UPOS1:	LDY	I,377		;SET ENTRY FLAG
	BMI	U1
UPOS2:	LDY	I,0
U1:	STY	Z,TEMP1
	AND	I,177		;AND OUT COLOR BIT
	LDX	I,0
U2:	CMP	AX,CTAB		;GET CORRECT DOMINO INDEX
	BEQ	U3
	INX
	CPX	I,12.
	BNE	U2
	LDA	I,SPOS1&377	;SOMETHING WRONG, CAN'T FIND DOMINO
	STA	Z,POS1		;RESET POSITIONS AND RETURN
	LDA	I,SPOS1&177400/256.
	STA	Z,POS1+1
	LDA	I,SPOS2&377
	STA	Z,POS2
	LDA	I,SPOS2&177400/256.
	STA	Z,POS2+1
	RTS
U3:	LDY	Z,TEMP1		;PLAYER 1 OR PLAYER 2?
	BPL	U6
	LDA	AX,OTABL	;PLAYER 1
	CLC			;UPDATE POSITION
	ADC	Z,POS1
	STA	Z,POS1
	LDA	AX,OTABH
	ADC	Z,POS1+1
	STA	Z,POS1+1
	RTS
U6:	LDA	AX,OTABL	;PLAYER 2
	CLC			;UPDATE POSITION
	ADC	Z,POS2
	STA	Z,POS2
	LDA	AX,OTABH
	ADC	Z,POS2+1
	STA	Z,POS2+1
	RTS
COIN:	JSR	A,KOIN		;GET COINS
	LDA	A,CSW
	BPL	CO1
	ROL
	BPL	CO2
	LDA	Z,COINS		;1 COIN 2 CREDITS
	BNE	S,+3
CO5:	RTS
	DEC	Z,COINS
	INC	Z,CREDIT
	JSR	A,TICK
	INC	Z,CREDIT
	JSR	A,TICK
	RTS
CO2:	LDA	Z,COINS		;1 COIN 1 CREDIT
	BEQ	CO5
	DEC	Z,COINS
	INC	Z,CREDIT
	JSR	A,TICK
	RTS
CO1:	LDA	Z,COINS		;2 COINS 1 CREDIT
	BEQ	CO5
	CMP	I,2
	BCC	CO5
	DEC	Z,COINS
	DEC	Z,COINS
	INC	Z,CREDIT
	JSR	A,TICK
	RTS
KOIN:	LDA	I,9.
	STA	Z,TEMP1
	STA	Z,TEMP1+1
K1:	BIT	A,COINAD
	BPL	S,+5
	BVC	S,+3
	RTS
	STA	A,TREST		;PULSE WATCHDOG
	LDY	I,372
	DEY
	BNE	S,-1
	BIT	A,COINAD
	BVC	S,+8.
	LDA	I,9.
	STA	Z,TEMP1
	BPL	K2
	LDA	Z,TEMP1
	BEQ	K2
	DEC	Z,TEMP1
	BNE	K2
	INC	Z,COINS
K2:	BIT	A,COINAD
	BPL	S,+8.
	LDA	I,9.
	STA	Z,TEMP1+1
	BPL	K1
	LDA	Z,TEMP1+1
	BEQ	K1
	DEC	Z,TEMP1+1
	BNE	K1
	INC	Z,COINS
	JMP	A,K1
LINE:	BIT	A,LFREQ		;CHECK 50/60 HZ BIT
	BMI	S,+3
	RTS
	BIT	A,AMODE
	BPL	S,-3
	BIT	A,AMODE
	BMI	S,-3
	LDA	I,10.		;WAIT FOR 18.5 MSEC
LI1:	LDY	I,0
	INY
	BNE	S,-1
	SEC
	SBC	I,1
	BNE	LI1
	BIT	A,AMODE		;CHECK POLARITY OF AC
	BMI	LI2		;IF NEGATIVE, RESET CREDIT
	RTS
LI2:	LDA	I,0
	STA	Z,CREDIT
	STA	Z,COINS
	RTS
NPOS1:	LDY	I,377
	BMI	N1
NPOS2:	LDY	I,0
N1:	STY	Z,TEMP1
	AND	I,177		;AND OUT COLOR BIT
	LDX	I,0
N2:	CMP	AX,KTAB		;FIND ARROW INDEX
	BEQ	N3
	INX
	CPX	I,4
	BNE	N2
	LDX	I,0
N3:	LDY	Z,TEMP1
	BPL	N6
	LDA	AX,PTABL
	CLC
	ADC	Z,POS1
	STA	Z,POS1
	LDA	AX,PTABH
	ADC	Z,POS1+1
	STA	Z,POS1+1
	RTS
N6:	LDA	AX,PTABL
	CLC
	ADC	Z,POS2
	STA	Z,POS2
	LDA	AX,PTABH
	ADC	Z,POS2+1
	STA	Z,POS2+1
	RTS
PTABL:	.BYTE	340,1,40,377
PTABH:	.BYTE	377,0,0,377
BTAB:	.BYTE	0,3,6,9.
CTAB:	.BYTE	50,52,42,54,56,46
	.BYTE	40,142,152,44,146,156
OTABL:	.BYTE	40,377,40,377,340,377
	.BYTE	340,1,340,1,40,1
OTABH:	.BYTE	0,377,0,377,377,377
	.BYTE	377,0,377,0,0,0
SCAN:	LDY	I,0
	LDX	I,0
	LDA	Z,AFLAG		;WHAT MODE?
	BEQ	SC1
	CMP	I,2
	BEQ	SC2
SC3:	LDA	AX,SW2		;GET LEFT SWITCHES
	BPL	SC4
	INX
	CPX	I,4
	BNE	SC3
	JMP	A,SC5
SC4:	LDA	Z,FLAG1		;PLAYER 1 STILL ACTIVE?
	BNE	SC5		;NO
	STX	Z,ADIR1
	LDA	AX,KTAB
	STA	Z,ACOD1
	STA	NY,POS1
SC5:	LDA	Z,AFLAG
	CMP	I,3
	BNE	SC6
SC2:	LDX	I,0
SC7:	LDA	AX,SW1		;GET RIGHT SWITCHES
	BPL	SC8
	INX
	CPX	I,4
	BNE	SC7
	JMP	A,SC9
SC8:	LDA	Z,FLAG2		;PLAYER 2 STILL ACTIVE?
	BNE	SC9		;NO
	STX	Z,ADIR2
	LDA	AX,KTAB
	ORA	I,200
	STA	Z,ACOD2
	STA	NY,POS2
SC9:	LDA	Z,AFLAG
	CMP	I,2
	BEQ	SC1
SC10:	RTS
SC1:	LDA	Z,TIMER		;ON AUTO-PLAY, ONLY
	BNE	SC11		;CHANGE ONCE PER MOVE
	LDA	Z,TOG
	BNE	SC11
	LDA	Z,FLAG1		;PLAYER 1 STILL ACTIVE?
	BNE	SC11		;NO
	JSR	A,NEXT1
SC11:	LDA	A,AFLAG
	CMP	I,2
	BEQ	SC10
SC6:	LDA	Z,TIMER
	BNE	SC10
	LDA	Z,TOG
	BEQ	SC10
	LDA	Z,FLAG2		;PLAYER 2 STILL ACTIVE?
	BNE	SC10		;NO
	JSR	A,NEXT2
	RTS
NEXT1:	LDY	I,377
	BMI	S,+4
NEXT2:	LDY	I,0
	STY	Z,TEMP2
	LDY	I,0		;CHOOSE STRATEGY
	LDA	Z,TAMT		;DEPENDENT ON GAME TIME
	CMP	I,10.
	BCS	NE60
	LDA	Z,TEMP2		;CHOOSE 2ND STRATEGY
	BPL	NE61		;ONLY IF COMPUTER
	LDA	Z,SCL1		;IS 2 OR MORE PTS.AHEAD
	STA	Z,TEMP3
	LDA	Z,SCR1
	BPL	NE62
NE61:	LDA	Z,SCR1
	STA	Z,TEMP3
	LDA	Z,SCL1
NE62:	SEC
	SBC	Z,TEMP3
	CMP	I,2
	BCS	NE60
	LDY	I,1
NE60:	STY	Z,SNUM
	LDA	I,0
	STA	Z,T1
	STA	Z,T2
	STA	Z,T3
	STA	Z,T4
	LDA	Z,TEMP2
	BMI	NE0
	LDA	Z,POS2
	SEC
	SBC	I,40
	STA	Z,PTR
	LDA	Z,POS2+1
	SBC	I,0
	STA	Z,PTR+1
	JMP	A,NE1
NE0:	LDA	Z,POS1
	SEC
	SBC	I,40
	STA	Z,PTR
	LDA	Z,POS1+1
	SBC	I,0
	STA	Z,PTR+1
NE1:	LDX	I,0		;LOOK AT SURROUNDING CELLS
	LDY	I,0		;IF NON-EMPTY SKIP CHECK LOGIC
	LDA	NY,PTR
	BNE	NE2		;IF EMPTY, CHECK ADJACENT
	LDA	Z,PTR		;4 CELLS TO GET AN EXIT COUNT
	SEC
	SBC	I,40
	STA	Z,TEMP1
	LDA	Z,PTR+1
	SBC	I,0
	STA	Z,TEMP1+1
	JSR	A,CHECK
NE2:	LDY	I,41
	INX
	LDA	NY,PTR
	BNE	NE3
	LDA	Z,PTR
	CLC
	ADC	I,1
	STA	Z,TEMP1
	LDA	Z,PTR+1
	ADC	I,0
	STA	Z,TEMP1+1
	JSR	A,CHECK
NE3:	LDY	I,100
	INX
	LDA	NY,PTR
	BNE	NE4
	LDA	Z,PTR
	CLC
	ADC	I,40
	STA	Z,TEMP1
	LDA	Z,PTR+1
	ADC	I,0
	STA	Z,TEMP1+1
	JSR	A,CHECK
NE4:	LDY	I,37
	INX
	LDA	NY,PTR
	BNE	NE5
	LDA	Z,PTR
	SEC
	SBC	I,1
	STA	Z,TEMP1
	LDA	Z,PTR+1
	SBC	I,0
	STA	Z,TEMP1+1
	JSR	A,CHECK
NE5:	LDY	I,0
NE6:	LDX	AY,T1		;GET STATISTICAL CONVERSION
	LDA	Z,SNUM		;CHOOSE PROPER STRATEGY
	BNE	NE40
	LDA	AX,STAB1
	BPL	NE41
NE40:	LDA	AX,STAB2
NE41:	STA	AY,T1
	INY
	CPY	I,4
	BNE	NE6
	LDX	Z,SNUM		;GET PROPER STRATEGY
	LDA	AX,SFOR
	STA	Z,TEMP1
	LDX	Z,ADIR2		;GIVE FORWARD
	LDA	Z,TEMP2		;DIRECTION AN
	BPL	S,+4		;ADDED BIAS
	LDX	Z,ADIR1
	LDY	I,0
	TYA
NE7:	CLC
	ADC	ZX,T1
	INY
	CPY	Z,TEMP1
	BNE	NE7
	STA	ZX,T1
	LDX	Z,SNUM		;GET PROPER STRATEGY
	LDA	AX,SCEN
	STA	Z,TEMP1
	LDX	I,1		;GIVE IMPETUS TOWARD CENTER
	LDA	Z,POS1
	LDY	Z,TEMP2
	BMI	S,+4
	LDA	Z,POS2
	AND 	I,20
	BEQ	S,+4
	LDX	I,3
	LDA	I,0
	TAY
NE30:	CLC
	ADC	ZX,T1
	INY
	CPY	Z,TEMP1
	BNE	NE30
	STA	ZX,T1
	LDX	I,0
	LDA	Z,POS1+1
	LDY	Z,TEMP2
	BMI	S,+4
	LDA	Z,POS2+1
	AND	I,2
	BNE	S,+4
	LDX	I,2
	LDA	I,0
	TAY
NE31:	CLC
	ADC	ZX,T1
	INY
	CPY	Z,TEMP1
	BNE	NE31
	STA	ZX,T1
	LDA	Z,T1
	CLC
	ADC	Z,T2
	CLC
	ADC	Z,T3
	CLC
	ADC	Z,T4
	STA	Z,TEMP1
	BEQ	NE50
	JSR	A,RANDU		;GET RANDOM #
NE8:	CMP	Z,TEMP1		;PUT RANDOM #
	BCC	NE9		;IN PROPER RANGE
	SEC
	SBC	Z,TEMP1
	JMP	A,NE8
NE9:	STA	Z,TEMP1
	LDY	I,0
	STY	Z,TEMP1+1
	LDA	Z,T1		;PICK DIRECTION
	BEQ	NE20
	CMP	Z,TEMP1
	BCS	NE10
	STA	Z,TEMP1+1
NE20:	INY
	LDA	Z,T2
	BEQ	NE21
	CLC
	ADC	Z,TEMP1+1
	CMP	Z,TEMP1
	BCS	NE10
	STA	Z,TEMP1+1
NE21:	INY
	LDA	Z,T3
	BEQ	NE22
	CLC
	ADC	Z,TEMP1+1
	CMP	Z,TEMP1
	BCS	NE10
NE22:	INY
NE10:	TYA
	LDY	Z,TEMP2
	BMI	NE13
	STA	Z,ADIR2
	TAX
	LDA	AX,KTAB
	ORA	I,200
	STA	Z,ACOD2
	LDY	I,0
	STA	NY,POS2
NE50:	RTS
NE13:	STA	Z,ADIR1
	TAX
	LDA	AX,KTAB
	STA	Z,ACOD1
	LDY	I,0
	STA	NY,POS1
	RTS
STAB1:	.BYTE	0,1,7.,1
STAB2:	.BYTE	0,1,12.,1
SFOR:	.BYTE	6,12.
SCEN:	.BYTE	5,1
CHECK:	LDY	I,0		;SCAN SURROUNDING CELLS
	LDA	NY,TEMP1	;EVERY EMPTY CELL
	BNE	S,+4		;BUMP COUNTER
	INC	ZX,T1
	LDY	I,41
	LDA	NY,TEMP1
	BNE	S,+4
	INC	ZX,T1
	LDY	I,100
	LDA	NY,TEMP1
	BNE	S,+4
	INC	ZX,T1
	LDY	I,37
	LDA	NY,TEMP1
	BNE	S,+4
	INC	ZX,T1
	RTS
;ROUTINE TO PRODUCE A PSEUDO-RANDOM 8-BIT NUMBER
RANDU:	LDA	I,0
	STA	Z,TEMP1+1
	LDA	Z,UP4
	ROL
	ROL
	ROL	Z,TEMP1+1
	LDA	Z,UP1
	AND	I,10
	LSR
	LSR
	LSR
	EOR	Z,TEMP1+1
	LSR
	ROL	Z,UP1
	ROL	Z,UP2
	ROL	Z,UP3
	ROL	Z,UP4
	LDA	Z,UP1		;CHECK FOR LOCK-UP
	BNE	RA1
	LDA	Z,UP2
	BNE	RA1
	LDA	Z,UP3
	BNE	RA1
	LDA	Z,UP4
	BNE	RA1
	LDA	I,42		;LOCK-UP OCCURRED
	STA	Z,UP3
RA1:	LDA	Z,UP3
	RTS
;RAM TEST
TRAM:	LDX	I,0		;FILL UP RAM WITH TEST PATTERN
TT1:	TXA
	CLC
	ADC	I,4
	STA	AX,2000
	TXA
	CLC
	ADC	I,5
	STA	AX,2400
	TXA
	CLC
	ADC	I,6
	STA	AX,3000
	TXA
	CLC
	ADC	I,7
	CPX	I,200		;DO NOT CHECK WORKING RAM
	BCS	S,+5
	STA	AX,3400
	INX
	BNE	TT1
TT2:	TXA			;NOW CHECK RAM CELLS
	CLC
	ADC	I,4
TT3:	CMP	AX,2000		;IF RAM CELL BAD,
	STA	A,TREST
	BNE	TT3		;HANG ON CELL
	TXA
	CLC
	ADC	I,5
TT4:	CMP	AX,2400
	STA	A,TREST
	BNE	TT4
	TXA
	CLC
	ADC	I,6
TT5:	CMP	AX,3000
	STA	A,TREST
	BNE	TT5
	TXA
	CLC
	ADC	I,7
	CPX	I,200
	BCS	TT40
TT6:	CMP	AX,3400
	STA	A,TREST
	BNE	TT6
TT40:	INX
	BNE	TT2
	LDA	I,0		;TURN OFF SOUND
	STA	Z,CRASH
	LDX	I,0
TT30:	JSR	A,WAIT
	DEX
	BNE	TT30
	JSR	A,BLANK		;BLANK SCREEN
	LDX	I,0		;ROM CHECKSUM
	LDA	I,PWRON&177400/256.
	STA	Z,PTR+1
TT8:	LDA	I,4
	STA	Z,TEMP1
	LDA	I,0
	STA	Z,PTR
	TAY
TT7:	CLC
	ADC	NY,PTR		;BUILD CHECKSUM
	INY
	STA	A,TREST		;PULSE WATCHDOG
	BNE	TT7
	INC	Z,PTR+1
	DEC	Z,TEMP1
	BNE	TT7
	CMP	AX,CKSUM
	BEQ	TT9
	TXA			;BAD ROM CHECKSUM
	ORA	I,60
	STA	AX,PLAYFIELD+1020	;OUTPUT ROM # TO SCREEN
TT9:	INX
	CPX	I,3
	BNE	TT8
	STA	A,AMODE		;TURN ON SOUND
	LDA	I,14
	STA	Z,SOUND
	STA	A,SB1+1		;TURN ON LITES
	STA	A,SB2+1
TT10:	LDX	I,0		;SWITCH TEST
	LDY	I,0
TT11:	LDA	AX,4030
	BMI	TT12
	LDY	I,6
TT12:	INX
	CPX	I,4
	BNE	TT11
	LDX	I,0
TT15:	LDA	AX,4050
	BMI	TT16
	LDY	I,6
TT16:	INX
	CPX	I,6
	BNE	TT15
	LDA	A,4100
	BMI	S,+4
	LDY	I,6
	ROL
	BMI	S,+4
	LDY	I,6
	STY	Z,CRASH
TT20:	LDA	A,AMODE
	ROL
	ROL
	BPL	TT20
	STA	A,TREST		;PULSE WATCHDOG
	LDA	I,0
	STA	Z,CRASH
	LDA	A,ST
	BPL	TT10
	JMP	A,PWRON
CKSUM:	.BYTE	213,343,62,140
KTAB:	.BYTE	76,77,74,75
TAB1:	.BYTE	50,50,52,52,52,40
	.BYTE	40,40,156,156,156,50
TAB2:	.BYTE	42,42,54,54,54,56
	.BYTE	56,56,44,44,44,42
TAB3:	.BYTE	50,50,46,46,46,40
	.BYTE	40,40,142,142,142,50
TAB4:	.BYTE	146,146,54,54,54,152
	.BYTE	152,152,44,44,44,146
NMI:	PHA
	TXA
	PHA
	CLD
	LDA	Z,FRAME
	BNE	S,+5
	STA	A,NREST		;RESET NOISE CIRCUIT
	LDA	Z,RTEST		;CHECK RAM CELLS
	CMP	I,222
	BNE	NM2
	LDA	Z,RTEST+1
	CMP	I,77
	BNE	NM2
	TSX
	LDA	AX,405		;GET PCH
	CMP	I,60		;VALID RANGE?
	BCC	NM3
	CMP	I,100
	BCS	NM3
NM1:	PLA
	TAX
	PLA
	RTI
NM2:	LDA	I,222
	STA	Z,RTEST
	LDA	I,77
	STA	Z,RTEST+1
NM3:	JMP	A,PWRON
	.BYTE	4		;DUMMY BYTE
	.EVEN
MESG:	.WORD	MES0
	.WORD	MES1
	.WORD	MES2
	.WORD	MES3
	.WORD	MES4
	.WORD	MES5
	.WORD	MES6
	.WORD	MES7
	.WORD	MES8
POSM:	.WORD	PLAYFIELD+54
	.WORD	PLAYFIELD+14
	.WORD	PLAYFIELD+210
	.WORD	PLAYFIELD+210
	.WORD	PLAYFIELD+210
	.WORD	PLAYFIELD+502
	.WORD	PLAYFIELD+1045
	.WORD	PLAYFIELD+1447
	.WORD	PLAYFIELD+712
MES0:	.BYTE	75
	.ASCII	/ SCORE /
	.BYTE	277,0
MES1:	.ASCIZ	/GAME OVER/
MES2:	.ASCIZ	/1 PLAY PER COIN/
MES3:	.ASCIZ	/2 PLAYS PER COIN/
MES4:	.ASCIZ	/2 COINS PER PLAY/
MES5:	.ASCIZ	/YOU HAVE CREDIT FOR   PLAYERS/
MES6:	.ASCIZ	/PUSH YOUR START BUTTON/
MES7:	.ASCIZ	/  POINTS WINS GAME/
MES8:	.ASCIZ	/EACH PLAYER/
	.REPT	37772-.
	.BYTE	0
	.ENDM
	.WORD	NMI,PWRON,PWRON
	.END
                                                                                                                                  